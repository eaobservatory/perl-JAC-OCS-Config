package JAC::OCS::Config::ACSIS::ProcessLayout;

=head1 NAME

JAC::OCS::Config::ACSIS::ProcessLayout - Parse OCS ACSIS process layout configurations

=head1 SYNOPSIS

  use JAC::OCS::Config::ACSIS::ProcessLayout;

  $cfg = new JAC::OCS::Config::ACSIS::ProcessLayout( DOM => $dom);

=head1 DESCRIPTION

This class can be used to parse and modify the ACSIS process layout
configuration information present in the C<process_layout> element of an
OCS configuration.

=cut

use 5.006;
use strict;
use Carp;
use warnings;
use XML::LibXML;

use JAC::OCS::Config::Error qw| :try |;

use JAC::OCS::Config::XMLHelper qw(
				   find_children
				   find_attr
				  );

use base qw/ JAC::OCS::Config::CfgBase /;

use vars qw/ $VERSION /;

$VERSION = 1.0;

=head1 METHODS

=head2 Constructor

=over 4

=item B<new>

Create a new ProcessLayout configuration object. An object can be created from
a file name on disk, a chunk of XML in a string or a previously created
DOM tree generated by C<XML::LibXML> (i.e. A C<XML::LibXML::Element>).

  $cfg = new JAC::OCS::Config::ACSIS::ProcessLayout( File => $file );
  $cfg = new JAC::OCS::Config::ACSIS::ProcessLayout( XML => $xml );
  $cfg = new JAC::OCS::Config::ACSIS::ProcessLayout( DOM => $dom );

A blank object will be created if no arguments are supplied.

=cut

sub new {
  my $self = shift;

  # Now call base class with all the supplied options +
  # extra initialiser
  return $self->SUPER::new( @_, 
			    $JAC::OCS::Config::CfgBase::INITKEY => { 
				                                    monitorInterfaces => {},
								   }
			  );
}

=back

=head2 Accessor Methods

=over 4

=item B<getMonitorInterfaces>

Return a list of names of the monitors used in this observation

  @monitors = $pl->getMonitorInterfaces();

=cut

sub getMonitorInterfaces {
  my $self = shift;
  return %{$self->{monitorInterfaces}};
}

=back

=head2 Class Methods

=over 4

=item B<getRootElementName>

Return the name of the _CONFIG element that should be the root
node of the XML tree corresponding to the ACSIS process layout config.

 @names = $h->getRootElementName;

=cut

sub getRootElementName {
  return( "process_layout" );
}

=back

=begin __PRIVATE_METHODS__

=head2 Private Methods

=over 4

=item B<_process_dom>

Using the C<_rootnode> node referring to the top of the Instrument XML,
process the DOM tree and extract the names of the monitors. Note that
it does not process all the information in the xml tree, because we
only need to expose the names and interfaces of the monitors. Because 
stringify is not implemented here, CfgBase will return the xml that was 
given to the constructor.

 $self->_process_dom;

Extracts the monitorIds and interfaces and stores them for later retrieval.

=cut

sub _process_dom {
  my $self = shift;

  my $el = $self->_rootnode;
  my $monitor_layout = find_children( $el, "monitor_layout", min => 1, max => 1);

  my @monitors = find_children( $monitor_layout, "monitor_process", min => 1);
  for my $monitor (@monitors) {
    my $id = find_attr( $monitor, "id");
    my $if = find_attr( $monitor, "interface");
    
    ${$self->{monitorInterfaces}}{$id} = $if;
  }
}

=back

=end __PRIVATE_METHODS__

=head1 XML SPECIFICATION

The ACSIS XML configuration specification is documented in
OCS/ICD/005 with a DTD available at
http://docs.jach.hawaii.edu/JCMT/OCS/ICD/005/acsis.dtd.

=head1 AUTHOR

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>

Copyright (C) 2008 Science and Technology Facilities Council.
Copyright 2004 Particle Physics and Astronomy Research Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful,but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 59 Temple
Place,Suite 330, Boston, MA  02111-1307, USA

=cut

1;
